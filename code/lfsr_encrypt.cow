lfsr_encrypt{
    preamble{
        >(127) ^ > ^ <(127)             # fetch the seed and tap
        > _ <  _                        # place the seed and tap
        <
        call{ run_encryption }
                              
    }
    depends{
        code/bitwise.cow
    }
    namespace{
        run_encryption{
            call{ finish }
            ZERO PUSH
            
            > 
            call{main}
            call{lfsr advance}
            << ZERO +(58) >>        # main message is 58 chars

            call{enter_main}

            > PUSH < PUSH           # grab the seed and tap

            >(70)                   # encrypt the first 5 chars in free space
            << ZERO +(4) >>         # set up to encrypt 5 chars

            POP > POP << PUSH >     # place seed,tap,push zero
            
            call{main}
            call{lfsr advance}
        }
        main{  # encrypt remaining message
            # seed and tap in +1, +2
            #    -3     -2     -1     +0     +1     +2
            # | ITER | ZERO |  CC  | NONE | SEED | TAP  |
            < 
            > PUSH > PUSH >> POP < POP < # in +2
            >>> [-] +(32)               # ascii space in +5
            PUSH > wipe(8) POP          # set up the prefix
            PUSH >(42)
            wipe(7) <(57)    # set up the suffix
            <<<                         # in +2
            <<<<  PUSH                  # loop 58 times, in -2
            >(3)                        # in +1
            >> PUSH                     # grab the tap in +3
            < PUSH                      # grab seed in +2
            >> PUSH << POP              # place next value in +2
            > POP                       # seed in +3
            <                           # rebase to +2
            call{ lfsr kickstart }             
        }
        finish{
            >(8)
            rsave(5) <(5)       # grab the initial values
            <(10)               # move back to end of encypted section
            +(56)               # set up counter for memshift loop
            call{cleanup}
            call{memshift4}
        }
        enter_main{
            PUSH > PUSH                 # grab seed and tap
            <(76) POP < POP
        }
        cleanup{
            # shift the last three values
            << PUSH < PUSH < PUSH
            >(5) POP > POP > POP
            <<<
            _<(4) _
            >(66)
        }
        memshift4{
            # counter in +0
            - PUSH > POP <
            [ # if nonzero
                call{ memshift4 }
                ZERO
            ]
            > PUSH << POP               # move counter over
            < PUSH >>>>> POP            # shift the next value
            <<<<

        }
        step_lfsr{
            call{lfsr advance}
        }
        encrypt_external{ # expose main encryption loop to importers
            # start config:
            #    +0     +1
            # | VAL  | SEED |
            # stack: ... COUNT TAP            
            < POP >                 # pop off the namespace null
            call{ lfsr kickstart } 
        }
        lfsr imports bitwise binds bitwise{
            kickstart{
                < POP >             # pop the zero from entering namespace
                call{encrypt_loop}
                call{encrypt}
            }
            transition{ # incoming: seed in +0, tap in +1
                #    -4     -3      -2     -1     +0     +1     +2
                # | DONE | COUNT | ZERO |  CC  | SEED | TAP  | NEXT |
                
                > PUSH                  # grab the tap
                > PUSH < POP            # move the next value into +1 position
                < PUSH >> POP           # place the seed in +2 for encryption
                <<< POP                 # tap in -1
                << PUSH                 # push COUNT to stack
                >> PUSH                 # push tap to stack
                < PUSH >> POP           # zero SEED cell
                >                       # position in +1 to shift frame

                call{encrypt_loop}
                call{encrypt}
            }
            encrypt_loop{ # value to encrypt at +0
                # stack: ... COUNT TAP
                #    -4     -3       -2     -1     +0     +1     +2
                # | TARG | FREE1 | FREE2 |  CC  | DONE | SEED | ---- |
                # advance lfsr
                # encrypt byte
                # how to maneuver everything with limited space

                PUSH <(4) POP           # place encrypted byte in TARG
                >(4) POP                # tap in DONE
                <(3) POP PUSH           # counter in FREE1
                >(1) POP                # counter in FREE2
                [
                    <->                 # decrement counter
                    >>
                    call{ transition }
                    call{ advance }
                    <<                  # clear dup counter in FREE2 to break
                    ZERO
                ]
                # shuffle things around for lfsr advance
                # target config:
                # |  CC  | SEED | TAP  |
                >>                      # move into DONE (tap is here)
                PUSH < POP >> PUSH < POP < PUSH >> POP <
                # ready for lfsr advancement
            }
            encrypt{ # value to encrypt in +0, seed in +1
                #     v
                # | VALUE | SEED  |

                call{bitwise xor}

                # result: encrypted in +0, seed in +1
                # | DONE  | SEED  |
                #     ^
            }
            advance{ # seed in +0, tap pattern in +1
                PUSH                    # save the seed
                prep_expand
                
                call{apply_tap}
                call{bitwise double_expand}
                # result: new seed in +0, tap in +1
            }
            apply_tap{
                # start config:
                # | |
                # stack: ... SEED
                >>>> # move to start of bit pairs
                bit_and(8)
                finish_advance
                < POP >                 
                PUSH < POP              # store the result in the control cell
                > POP [-<++>]           # double original value into -1 (shleft)
                < PUSH > POP            # copy cc value into +0
            }
        }
    }
    bindings{
        rightAndPop{
            > POP
        }
        toggle_xor{
            # start:    v
            # | zero | AND | toggle |

            # [>[<<->>]<-]>[<<+>>-]<<
            [->-<]>[<<+>> ZERO ]<<<
        }
        finish_advance{
            <                       # end of last pair
            <<                      # last pair result
            # EXIT
            toggle_xor(7)
            > PUSH <<<< POP         # place the result in +0
            >(19) lrestore(19)      # restore saved cells
            < PUSH >                
        }
        wipe{
            _^>
        }
        rsave{
            PUSH >
        }
    }
    postamble{

    }
}